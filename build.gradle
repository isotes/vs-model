plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

group = 'io.github.isotes'
version = '1.0.0'

sourceCompatibility = 1.6

repositories {
	jcenter()
}


def xmlBeansSchemaDir = 'src/main/resources/'
def xmlBeansSources = "${buildDir}/generated-src"
def xmlBeansResources = "${buildDir}/generated-res"

configurations {
	xmlbeans
}

task generateXmlBeans() {
	inputs.dir xmlBeansSchemaDir
	outputs.dir xmlBeansSources
	dependsOn configurations.xmlbeans.getTaskDependencyFromProjectDependency(true, 'compileJava')
	doLast {
		ant.taskdef(name:'xmlbean', classname: 'org.apache.xmlbeans.impl.tool.XMLBean', classpath: configurations.xmlbeans.asPath)
		ant.xmlbean(schema: xmlBeansSchemaDir,
				srcgendir: xmlBeansSources,
				javasource: '1.5',  // as of XMLBeans 3.1.0, anything but 1.5 is treated as 1.4 (just Arrays, no Lists)
				srconly: true,
				noupa: true,
				classgendir: xmlBeansResources,
				typesystemname: 'io.github.isotes.vs.model.typesystemholder',
				excludes: 'Microsoft*.xsd'
		)
	}
}

sourceSets.main.java.srcDirs += xmlBeansSources
sourceSets.main.resources.srcDirs += xmlBeansResources
compileJava {
	dependsOn(generateXmlBeans)
}


dependencies {
	xmlbeans 'org.apache.xmlbeans:xmlbeans:3.1.0'
	api 'org.apache.xmlbeans:xmlbeans:3.1.0'
}


javadoc {
	source = sourceSets.main.allJava
	// JavaDoc for XMLBeans 3.1.0 is not online
	options.with {
		links 'https://docs.oracle.com/javase/8/docs/api/', 'https://xmlbeans.apache.org/docs/3.0.0/reference/'
	}
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}


task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
	from javadoc
	archiveClassifier = 'javadoc'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			pom {
				name = 'vs-model'
				description = 'Generated XMLBeans Java classes for the XML schema of MSBuild project files'
				url = 'https://github.com/isotes/vs-model'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/mit-license'
					}
				}
				developers {
					developer {
						name = 'Robert Sauter'
						email = 'isotes@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/isotes/vs-model.git'
					developerConnection = 'scm:svn:https://github.com/isotes/vs-model.git'
					url = 'https://github.com/isotes/vs-model'
				}
			}
		}
	}
	repositories {
		maven {
			if (project.hasProperty('publishOssrh') && publishOssrh) {
				def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
				def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
				url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
				authentication {
					basic(BasicAuthentication)
				}
				credentials {
					username = ossrhUsername
					password = ossrhPassword
				}
			} else {
				def releasesRepoUrl = "$buildDir/repos/releases"
				def snapshotsRepoUrl = "$buildDir/repos/snapshots"
				url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			}
		}
	}
}

signing {
	required { !version.endsWith('SNAPSHOT') }
	if (required) {
		sign publishing.publications.mavenJava
	}
}
